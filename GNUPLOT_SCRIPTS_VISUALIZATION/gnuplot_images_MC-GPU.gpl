#!/usr/bin/gnuplot 

# -- Represent the 2D image simulated with MC-GPU:
#    The name of the data file must be "mc-gpu_image.dat".
#     To use log scale I can calculate "-log10($1)" or press "l" after clicking the graphic.

#set yrange[0: 349]
#set xrange[0:1299]
#set zrange[0:0.3]
#set cbrange[0:0.3]

set size ratio -1
# Determine voxel color from corner left-bottom:
set pm3d map corners2color c1
set palette gray negative


# set xlabel "X (pixel)"
# set ylabel "Z (pixel)"

set title "Simulated image: ALL particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using ($1+$2+$3+$4) notitle
pause -1 "  --Press enter for next image..."

set title "Simulated image: NON-SCATTERED particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 1 notitle
pause -1 "  --Press enter for next image..."

set title "Simulated image: ALL SCATTER [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using ($2+$3+$4) notitle
pause -1 "  --Press enter for next image..."

set title "Simulated image: COMPTON particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 2 notitle
pause -1 "  --Press enter for next image..."

set title "Simulated image: RAYLEIGH particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 3 notitle
pause -1 "  --Press enter for next image..."

set title "Simulated image: MULTIPLE-SCATTERED particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 4 notitle


pause -1 "  --Press enter to save images in PNG format and exit... "


#########################
#  OUTPUT png IMAGES:

#set autoscale

set terminal png large size 2000,1000 crop
# set terminal postscript eps

set output 'mc-gpu_image.png'
set title "Simulated image: ALL particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using ($1+$2+$3+$4) notitle

set output 'mc-gpu_image_NonScattered.png'
set title "Simulated image: NON-SCATTERED particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 1 notitle

set output 'mc-gpu_image_AllScatter.png'
set title "Simulated image: ALL SCATTER [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using ($2+$3+$4) notitle

set output 'mc-gpu_image_Compton.png'
set title "Simulated image: COMPTON particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 2 notitle

set output 'mc-gpu_image_Rayleigh.png'
set title "Simulated image: RAYLEIGH particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 3 notitle

set output 'mc-gpu_image_MultipleScatter.png'
set title "Simulated image: MULTIPLE-SCATTERED particles [eV/cm^2 per x-ray]"
splot "mc-gpu_image.dat" using 4 notitle

pause 0.1 "  --Done! "
 
